class {
  constant_pool {
    1: ClassInfo "PutGetStatic"
    2: ClassInfo "java/lang/Object"
    3: NameAndType "F2" "D"
    4: NameAndType "F1" "I"
    5: FieldRef #1 #3
    6: FieldRef #1 #4

    auto: "main"
    auto: "ok"
    auto: "wrong_type"
    auto: "wrong_type2"
    auto: "wrong_idx"
    auto: "wrong_idx2"
    auto: "([Ljava/lang/String;)I"
    auto: "F2"
    auto: "D"
    auto: "F1"
    auto: "I"
  }

  Name: #1
  Super: #2

  fields {
    public static "I": "F1"
    public static "D": "F2"
  }

  // Should pass verification
  method "ok" "([Ljava/lang/String;)I" {
    Flags: public, static
    MaxStack: 2
    MaxLocals: 1

    bytecode {
      dconst_1
      putstatic #5 // Field F2:D
      getstatic #6 // Field F1:I
      ireturn
    }
  }

  // Putstatic tries to save the wrong type
  method "wrong_type" "([Ljava/lang/String;)I" {
    Flags: public, static
    MaxStack: 2
    MaxLocals: 1

    bytecode {
      dconst_1
      putstatic #6 // Field F1:I
      getstatic #6 // Field F1:I
      ireturn
    }
  }

  // Getstatic pushes the wrong type
  method "wrong_type2" "([Ljava/lang/String;)I" {
    Flags: public, static
    MaxStack: 2
    MaxLocals: 1

    bytecode {
      getstatic #5 // Field F2:D
      ireturn
    }
  }

  // Wrong cp index
  method "wrong_idx" "([Ljava/lang/String;)I" {
    Flags: public, static
    MaxStack: 1
    MaxLocals: 1

    bytecode {
      putstatic #3
    }
  }

  // Wrong cp index
  method "wrong_idx2" "([Ljava/lang/String;)I" {
    Flags: public, static
    MaxStack: 1
    MaxLocals: 1

    bytecode {
      getstatic #3
    }
  }
}
